# script to generate graphs from a load test done by Bees with Machine Guns
#
# usage:
# gnuplot -e "filename='<filename w/o csv extension>'" LoadTest.gpi
#

# output to a jpeg file
set terminal jpeg size 1440,900

# This sets the aspect ratio of the graph
set size 1, 1
set lmargin 10
set rmargin 10

set output filename.'.jpg'

# Where to place the legend/key
set key left top

set multiplot layout 2, 1 title filename

# Draw gridlines oriented on the y axis
set grid y
# Label the x-axis
set xlabel 'Concurrent Users'
# Tell gnuplot to use commas as the delimiter instead of spaces (default)
set datafile separator ','
set key autotitle columnhead

#
# first graph
#
set title "Requests/Second and % Errors"
set ytics nomirror 
set y2tics 
set ylabel 'Requests/Second' 
set format y2 "%g %%"

# Plot the data
plot filename.'.csv' using 1:7 with lines lt 5 lw 3 axes x1y1, \
             ''      using 1:8 with lines lt 2 lw 3 axes x1y1, \
             ''      using 1:($6*100) with lines lt 1 lw 3 axes x1y2
unset y2tics 
unset y2label

#
# second graph
#
set title "Response Time"
set ylabel "ms"

set bars 4.0
set style fill solid

# Plot the data
plot filename.'.csv' using 1:15:9:32:31 with candlesticks lt 2 title 'Min/P10/Med/P90/P95' whiskerbars 0.6, \
          ''         using 1:12:12:12:12 with candlesticks lt -1 notitle,\
          ''         using 1:11 with lines lt -1 lw 3
