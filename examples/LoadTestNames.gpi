# script to generate graphs from a load test done by Bees with Machine Guns
#
# usage:
# gnuplot -e "filename='<filename w/o csv extension>'" LoadTest.gpi
#

# output to a jpeg file
set terminal jpeg size 1440,900

# This sets the aspect ratio of the graph
set size 1, 1
set lmargin 12
set rmargin 10

set output filename.'.jpg'

# Where to place the legend/key
set key left top

set multiplot layout 2, 1 title filename font "Bold,20"

# Draw gridlines oriented on the y axis
set grid y
# Label the x-axis
set xlabel 'Iteration'
#set xlabel 'Concurrent Requests'
# Tell gnuplot to use commas as the delimiter instead of spaces (default)
set datafile separator ','
set key autotitle columnhead

#
# first graph
#
set title "Requests/Second(green) and % Errors(red)" font "Bold,14"
set ytics nomirror
set y2tics
set ylabel 'Requests/Second' textcolor lt 2
set y2label 'Error Percentage' textcolor lt 1
set decimal locale
#set format "%'.0f"
set format "%'g"
set format y2 "%g %%"
set yrange [0:]
set y2range [0:10]
#set boxwidth 0.5
#set style fill solid

# Plot the data
plot filename.'.csv' using 1:8:xtic(2) with lines lt 5 lw 3 axes x1y1, \
             ''      using 1:($8-($8-$9)/2) with lines lt 2 lw 3 axes x1y1, \
             ''      using 1:($7*50) with lines lt 1 lw 3 axes x1y2
# the creative arithmetic above is done in order to overcome a bug in ab in which it counts each error twice, and since successful hits are calculated as total-bad it also has to be fixed.
unset y2tics
unset y2label
set yrange [*:*]
#unset boxwidth

#
# second graph
#
set title "Response Time" font "Bold,14"
unset ylabel
set ylabel "ms"

set bars 4.0
set style fill solid

# Plot the data
plot filename.'.csv' using 1:16:10:33:32:xtic(2) with candlesticks lt 2 title 'Min/P10/Med/P90/P95' whiskerbars 0.6, \
          ''         using 1:13:13:13:13 with candlesticks lt -1 notitle,\
          ''         using 1:12 with lines lt -1 lw 3
